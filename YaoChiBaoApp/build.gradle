// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.28-alpha'
        classpath 'com.karumi:shot:5.10.4'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    defaultBaseUrl = "\"https://www.yaochibao.com/api/v1/\""
    baseUrl = [
            dev : "\"http://10.0.2.2:3000/\"",
            prod: "\"https://www.yaochibao.com/api/v1/\""
    ]

    getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    getBaseUrl = { project ->
        if (!project.hasProperty('env')) {
            println("env param is not exist. use defaultBaseUrl:${defaultBaseUrl}")
            return defaultBaseUrl
        }
        def baseUrl = project.baseUrl[project.property('env')]
        if (baseUrl != null) {
            defaultBaseUrl = baseUrl
            println("env ${project.property('env')} url: ${defaultBaseUrl}")
        } else {
            println("env ${project.property('env')} is not exist. use defaultBaseUrl:${defaultBaseUrl}")
        }
        return defaultBaseUrl
    }
}